plugins {
    id 'scala'
}

group 'com.kemling'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: "io.gatling.highcharts", name: "gatling-charts-highcharts", version:"3.3.1"
}


/**
 * Gatling load tests
 */
task testLoad(type: JavaExec) {
    description = 'Test load the Spring Boot web service with Gatling'
    group = 'Load Test'
    classpath = sourceSets.test.runtimeClasspath
    jvmArgs = [
            "-Dlogback.configurationFile=${logbackGatlingConfig()}",
            "-Dgatling.data.console.writePeriod=60",
            "-Dgatling.charting.indicators.lowerBound=10",
            "-Dgatling.charting.indicators.higherBound=20"
    ]
    main = 'io.gatling.app.Gatling'
    args = [
            '--simulation', 'gatling.simulation.LoadTest',
            '--results-folder', "${buildDir}/gatling-results",
            '--binaries-folder', sourceSets.test.output.classesDirs.toString()
    ]
}

def logbackGatlingConfig() {
    return sourceSets.test.resources.find { it.name == 'logback-gatling.xml'}
}